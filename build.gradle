buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
    }
}

group 'br.com.pedront'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.RELEASE'
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.22'
            dependency 'ma.glasnost.orika:orika-core:1.5.2'

            dependencySet(group: 'io.springfox', version: '2.7.0') {
                entry 'springfox-swagger-ui'
                entry 'springfox-swagger2'
            }
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    }
}

project(':sync-server') {
    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
        compile 'ma.glasnost.orika:orika-core'
        compile 'io.springfox:springfox-swagger-ui'
        compile 'io.springfox:springfox-swagger2'
    }
}

project(':sync-client') {
    dependencies {
        compile 'org.springframework.boot:spring-boot-starter'
        compile 'org.springframework.cloud:spring-cloud-starter-openfeign'

        compile project(':sync-server')
    }
}